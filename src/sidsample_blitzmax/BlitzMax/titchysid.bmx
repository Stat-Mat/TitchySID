' Load SID file from a resource
Const SID_RESOURCE    = 0

' Load SID file from provided memory
Const SID_MEMORY      = 1

' Options used in SIDOpen()
' Play Default sub song, as found in the PSID header
Const SID_DEFAULT     = 1

' Play specified sub song
Const SID_NON_DEFAULT = 2

'===================================================================
' This structure holds all of the SID file info after a successful
' call to SIDOpen().
'
' Note: Needs to be 4 byte aligned for C etc (hence the pad byte)
'===================================================================

Type props

    ' The address where the SID data should be placed in the C64's memory
    Field load_addr:Short
    
    ' The address in the C64's memory where the initialise routine for the
    ' SID songs is located (accepts a subsong ID in the accumulator)
    Field init_addr:Short

    ' The address in the C64's memory where the play routine is located. This
    ' is called frequently To produce continuous sound.
    Field play_addr:Short

    ' The total number of subsongs contained in the SID file
    Field num_songs:Byte
    
    ' The Default subsong To be played
    Field default_song:Byte

    ' The song speed
    Field speed:Byte

    ' Just alignment padding
    Field pad:Byte

    ' The total size of the SID data (in bytes)
    Field data_size:Short

    ' The name of the SID file
    Field sid_name:String
    
    ' The author's name
    Field author:String

    ' The copyright String
    Field copyright:String

End Type

Extern "win32"

    '===================================================================
    ' SIDOpen()
    '  
    ' Purpose: Open the SID library
    '
    '          Call with *either* a resource ID for the
    '          SID music or a pointer to a buffer in memory
    '          e.g.:
    '
    '          To load a resource within the executable:
    '          
    '          SIDOpen (res_id, 0, SID_RESOURCE, SID_DEFAULT, subsong);
    '
    '          To load from a buffer in memory:
    ' 
    '          SIDOpen (mem, mem_len, SID_MEMORY, SID_DEFAULT, subsong);
    '
    '          Last parameter is subsong (many PSID files contain several
    '          subsongs or sound effects). This is ignored if the options
    '          parameter has the SID_DEFAULT flag set. If you wish to
    '          specify which sub song to play, then you should use the
    '          SID_NON_DEFAULT flag instead.o play,
    '          then you should use the SID_NON_DEFAULT flag instead.
    '
    ' Returns non-zero on success
    '====================================================================

    Function SIDOpen:Int(res_mem:Byte Ptr, mem_len, mode:Byte, options:Byte, subsong:Byte)

    '===================================================================
    '  SIDClose()
    '
    '  Purpose : Close the SID library
    '
    '  Call this in your WM_CLOSE handler
    '===================================================================

    Function SIDClose()

    '=================================================
    ' The functions below are only available if the
    ' library is built with SID_EXTRAS enabled
    '=================================================

    '===================================================================
    ' SIDPlay()
    '
    ' Purpose : Start the SID playback
    '===================================================================

    Function SIDPlay()

    '===================================================================
    ' SIDStop()
    '
    ' Purpose : Stop the SID playback
    '===================================================================

    Function SIDStop()

    '===================================================================
    ' SIDPause()
    '
    ' Purpose : Pause the currently playing SID song
    '===================================================================

    Function SIDPause()

    '===================================================================
    ' SIDResume()
    '
    ' Purpose : Resume playing the SID song after a pause
    '===================================================================

    Function SIDResume()

    '===================================================================
    ' SIDChangeSong()
    '
    ' Purpose : Change to another sub song in the currently playing SID
    '		        file
    '
    '  Parameter : New subsong ID
    '===================================================================

    Function SIDChangeSong(subsong:Byte)

    '===================================================================
    ' SIDGetFFTData()
    '
    ' Purpose : Perform a fast fourier transform (FFT) of the current
    '           2048 16-bit samples generated by the SID emulation. This
    '           is typically useful for a spectrum analyser.
    '
    ' Parameter : A pointer to an array of 1024 4-byte floating point
    '             values. Upon return, each item in the array will
    '             contain a representation of amplitude at a particular
    '             frequency band. These values will range between 0 and
    '             1.
    '===================================================================

    Function SIDGetFFTData(pFFT:Float Ptr)

    '===================================================================
    ' SIDGetProps()
    '
    ' Purpose : Return the properties structure containing all of the
    '           information associated with the SID tune loaded with
    '           SIDOpen().
    '
    ' Parameter : A pointer to a memory buffer of suffcient size to
    '             hold a copy of the props structure defined above.
    '===================================================================

    Function SIDGetProps(sid_props:Byte Ptr)

EndExtern

' Function to allow mapping from the returned
' C struct To the BlitzMax Type object
Function GetProps(sid_props:props)
    Local mem:Byte Ptr
    Local memptr:Byte Ptr

    mem = MemAlloc(256)
    SIDGetProps(mem)

    MemCopy(Varptr(sid_props.load_addr), mem, 12)
    
    memptr = mem + 12
    sid_props.sid_name = String.FromCString(memptr)
    
    memptr = memptr + 32
    sid_props.author = String.FromCString(memptr)
    
    memptr = memptr + 32
    sid_props.copyright = String.FromCString(memptr)
    
    MemFree(mem)    
EndFunction
